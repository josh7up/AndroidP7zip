apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.ext.configuration.compileSdk
    buildToolsVersion rootProject.ext.configuration.buildTools

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.configuration.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a'

                cFlags '-I' + file('src/main/jni/p7zip/CPP'),
                       '-I' + file('src/main/jni/p7zip/CPP/include_windows')

                cppFlags '-I' + file("src/main/jni"),
                         '-I' + file("src/main/jni/p7zip/CPP"),
                         '-I' + file("src/main/jni/p7zip/CPP/Common"),
                         '-I' + file("src/main/jni/p7zip/CPP/include_windows"),
                         '-I' + file("src/main/jni/p7zip/CPP/myWindows"),
                         "-DANDROID_NDK",
                         "-fexceptions",
                         "-DNDEBUG",
                         "-D_REENTRANT",
                         "-DENV_UNIX",
                         "-DBREAK_HANDLER",
                         "-DUNICODE",
                         "-D_UNICODE",
                         "-DUNIX_USE_WIN_FILE",
                         "-fPIC",
                         '-DNDK_DEBUG'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${rootProject.ext.configuration.supportLibrary}"
}

apply from: 'https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/1.0.0/gradle-android-javadocs.gradle'

def myGroupId = 'com.github.josh7up'
def myArtifactId = 'AndroidP7zip'
def myVersionNumber = '1.0.1'
def myUsername = 'josh7up'
def myGitUrl = 'https://github.com/josh7up/AndroidP7zip'

group = myGroupId
version = myVersionNumber
// Important for ensuring this library project is not uploaded to bintray as a sub-project.
archivesBaseName = myArtifactId

install {
    repositories.mavenInstaller {
        pom.project {
            name myArtifactId
            description 'An Android compress and extract library, P7Zip port for Android.'
            url myGitUrl
            inceptionYear '2017'

            packaging 'aar'
            groupId myGroupId
            artifactId myArtifactId
            version myVersionNumber

            licenses {
                license {
                    name 'GNU LESSER GENERAL PUBLIC LICENSE 2.1'
                    url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                    distribution 'repo'
                }
            }
            scm {
                connection "${myGitUrl}.git"
                url myGitUrl

            }
            developers {
                developer {
                    id = myUsername
                    name 'Josh'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = myArtifactId
        licenses = ['LGPL-2.1']
        vcsUrl = "${myGitUrl}.git"
        publish = true
        version {
            name = myVersionNumber
            desc = "${myArtifactId} ${myVersionNumber}"
            released = new Date()
            vcsTag = myVersionNumber
        }
    }
}
