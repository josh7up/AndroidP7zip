apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.configuration.compileSdk
    buildToolsVersion rootProject.ext.configuration.buildTools

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.configuration.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'

                cFlags '-I' + file('src/main/jni/p7zip/CPP'),
                       '-I' + file('src/main/jni/p7zip/CPP/include_windows')

                cppFlags '-I' + file("src/main/jni"),
                         '-I' + file("src/main/jni/p7zip/CPP"),
                         '-I' + file("src/main/jni/p7zip/CPP/Common"),
                         '-I' + file("src/main/jni/p7zip/CPP/include_windows"),
                         '-I' + file("src/main/jni/p7zip/CPP/myWindows"),
                         "-DANDROID_NDK",
                         "-fexceptions",
                         "-DNDEBUG",
                         "-D_REENTRANT",
                         "-DENV_UNIX",
                         "-DBREAK_HANDLER",
                         "-DUNICODE",
                         "-D_UNICODE",
                         "-DUNIX_USE_WIN_FILE",
                         "-fPIC",
                         '-DNDK_DEBUG'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${rootProject.ext.configuration.supportLibrary}"
}

apply from: 'https://raw.githubusercontent.com/josh7up/AndroidMavenBintray/1.0.1/gradle-maven-bintray.gradle'

def groupId = 'com.github.josh7up'
def artifactId = 'AndroidP7zip'
def versionNumber = '1.0.2'
def gitUrl = 'https://github.com/josh7up/AndroidP7zip'

configureMavenInstall([
        groupId: groupId,
        artifactId: artifactId,
        versionNumber: versionNumber,
        developerId: 'josh7up',
        gitUrl: gitUrl,
        projectDescription: 'An Android compress and extract library, P7Zip port for Android.',
        projectInceptionYear: 2017,
        licenseName: 'GNU LESSER GENERAL PUBLIC LICENSE 2.1',
        licenseUrl: 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html',
        developerName: 'Josh Feldman'
])

File bintrayPropertiesFile = project.rootProject.file('bintray.properties')
Properties bintrayProperties = new Properties()
if (bintrayPropertiesFile.exists()) {
    bintrayProperties.load(bintrayPropertiesFile.newDataInputStream())
}

configureBintrayUpload(
        user: bintrayProperties.getProperty('user'),
        key: bintrayProperties.get('key'),
        repoName: 'android',
        artifactId: artifactId,
        licenseId: 'LGPL-2.1',
        gitUrl: gitUrl,
        versionNumber: versionNumber
)